apply plugin: 'java'
apply plugin: 'maven-publish'

group = 'com.github.signed.sandboxes.gradle.publish'
version = '1.0-SNAPSHOT'

dependencies {
    compile(
              'com.google.guava:guava:13.0-rc1'
            , 'commons-io:commons-io:2.3'
    )
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
}

publishing {
    publications {
        jars(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "source"
            }

            artifact javadocJar {
                classifier "javadoc"
            }

            pom.withXml {
                def versionNode = asNode().version

                versionNode.plus{
                    resolveStrategy = DELEGATE_ONLY
                    name 'New gradle publish api dry run'
                    description 'Just a dry run.'
                    url 'https://github.com/signed/sandboxes'
                    inceptionYear '2013'

                    scm {
                        url 'https://github.com/signed/sandboxes'
                        connection 'scm:https://github.com/signed/sandboxes.git'
                        developerConnection 'scm:git@github.com:signed/sandboxes.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'head'
                            name 'Head Master'
                        }
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url "http://localhost:8081/nexus/content/repositories/snapshots/"
            credentials {
                username nexusUsername
                password nexusPassword
            }
        }
    }
}

